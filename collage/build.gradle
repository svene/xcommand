plugins {
	id 'buildlogic.java-library-conventions'
	alias(libs.plugins.versions)
	id "org.xcommand.javacc"
	alias(libs.plugins.errorprone)
	alias(libs.plugins.spotless)
}

javacc {
	inputFile = file("src/main/javacc/templateparser.jj")
	genDir    = file("build/src/main/java/org/collage/jcc")
}
compileJava.dependsOn(jcc)

import net.ltgt.gradle.errorprone.CheckSeverity
tasks.withType(JavaCompile) {
	options.errorprone {
		//disableAllChecks
		check("NullAway", CheckSeverity.ERROR)
		option("NullAway:AnnotatedPackages", "org.xcommand")
		option("NullAway:ExcludedClassAnnotations", "javax.annotation.processing.Generated")
		option("NullAway:KnownInitializers", "org.xcommand.web.WebCVInitializationFilter.init")
		excludedPaths.set(".*/build/.*")
	}
	// Include to disable NullAway on test code
	if (name.toLowerCase().contains("test")) {
		options.errorprone {
			disable("NullAway")
		}
	}
}

spotless {
	java {
		palantirJavaFormat()
		targetExclude("build/**")
	}
}

dependencies {
	annotationProcessor libs.errorprone.core
	errorprone libs.nullaway
	api libs.jspecify
	errorprone libs.errorprone.core

	implementation project(':xc-core')
	implementation libs.javacc
	implementation libs.javassist

	implementation libs.slf4j.api
	implementation libs.slf4j.simple

	annotationProcessor libs.lombok
	compileOnly libs.lombok
	testAnnotationProcessor libs.lombok
	testCompileOnly libs.lombok

	testImplementation libs.mockito.core
	testImplementation libs.mockito.junit.jupiter

	implementation libs.jool
	testImplementation libs.assertj.core
}

sourceSets {
	main {
		java {
			srcDir 'build/src/main/java'
		}
	}
}

// ben-manes version-plugin:
def isNonStable = { String version ->
	def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
	rejectVersionIf {
		isNonStable(it.candidate.version)
	}
}
