plugins {
	id 'buildlogic.java-library-conventions'
	alias(libs.plugins.versions)
	id "org.xcommand.javacc"
	alias(libs.plugins.errorprone)
}

javacc {
	inputFile = file("src/main/javacc/jst.jj")
	genDir    = file("build/src/java/org/xcommand/template/jst/parser")
}
compileJava.dependsOn(jcc)

import net.ltgt.gradle.errorprone.CheckSeverity
tasks.withType(JavaCompile) {
	options.errorprone {
		//disableAllChecks
		check("NullAway", CheckSeverity.ERROR)
		option("NullAway:AnnotatedPackages", "org.xcommand")
		option("NullAway:ExcludedClassAnnotations", "javax.annotation.processing.Generated")
		option("NullAway:KnownInitializers", "org.xcommand.web.WebCVInitializationFilter.init")
		excludedPaths.set(".*/build/.*")
	}
	// Include to disable NullAway on test code
	if (name.toLowerCase().contains("test")) {
		options.errorprone {
			disable("NullAway")
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/src/java'
		}
	}
}
dependencies {
	annotationProcessor libs.errorprone.core
	errorprone libs.nullaway
	api libs.jspecify
	errorprone libs.errorprone.core

	implementation project(':xc-core')
	implementation libs.javacc
	annotationProcessor libs.recordbuilder.processor
	compileOnly libs.recordbuilder.core

	implementation libs.slf4j.api
	implementation libs.slf4j.simple

	annotationProcessor libs.lombok
	compileOnly libs.lombok
	testAnnotationProcessor libs.lombok
	testCompileOnly libs.lombok

	implementation libs.jool
	implementation libs.janino.janino
	implementation libs.servlet.api

	testImplementation libs.jimfs

	testImplementation libs.mockito.core
	testImplementation libs.mockito.junit.jupiter

	testImplementation libs.assertj.core
}

test {
	systemProperty 'eu.javaspecialists.books.dynamicproxies.util.MethodTurboBooster.disabled', 'true'
}

// ben-manes version-plugin:
def isNonStable = { String version ->
	def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
	rejectVersionIf {
		isNonStable(it.candidate.version)
	}
}
