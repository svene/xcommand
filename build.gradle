plugins {
	id("java-library")
	id "com.github.ben-manes.versions" version "0.46.0"
}
project.ext {
	VERSION_JAVACC = '7.0.12'
	DEP_SERVLET_API = 'jakarta.servlet:jakarta.servlet-api:6.0.0'
	DEP_JANINO = 'org.codehaus.janino:janino:3.1.9'
	DEP_JAVASSIST = 'org.javassist:javassist:3.29.2-GA'
	DEP_JAVACC = "net.java.dev.javacc:javacc:${VERSION_JAVACC}"

	DEP_GROOVY = 'org.apache.groovy:groovy:4.0.11'
	DEP_LOMBOK_VERSION = '1.18.26'
	DEP_SPOCK_BOM = 'org.spockframework:spock-bom:2.3-groovy-4.0'
	DEP_SPOCK_CORE = 'org.spockframework:spock-core'
	DEP_JOOL = 'org.jooq:jool:0.9.15'
	DEP_VAVR = 'io.vavr:vavr:0.10.4'
}

allprojects {
//	apply plugin: 'java'
	apply plugin: 'groovy'

	repositories {
		mavenCentral()
	}

	dependencies {
		annotationProcessor 'io.soabase.record-builder:record-builder-processor:36'
		compileOnly 'io.soabase.record-builder:record-builder-core:36'

		implementation 'org.slf4j:slf4j-api:2.0.7'
		implementation 'org.slf4j:slf4j-simple:2.0.7'
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
		testImplementation 'org.assertj:assertj-core:3.24.2'
		testImplementation "org.mockito:mockito-core:5.3.0"
		testImplementation "org.mockito:mockito-junit-jupiter:5.3.0"

        testImplementation 'com.google.jimfs:jimfs:1.2'
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(20)
		}
	}
	tasks.withType(JavaCompile) {
		options.deprecation = true
	}

	task ('copyToLib', type: Copy) {
		into('build/_artifacts')
		from configurations.compileClasspath
		from configurations.default.allArtifacts*.file
		from configurations.compileClasspath.allArtifacts*.file
	}

	task renameJCC(type: Copy, dependsOn: 'copyToLib') {
		from("build/_artifacts/javacc-${VERSION_JAVACC}.jar")
		into('build/_artifacts')
		rename("javacc-${VERSION_JAVACC}.jar", 'javacc.jar')
	}

	test {
		useJUnitPlatform {
//        	includeEngines 'junit-jupiter' // enabling this line causes spock tests not to be executed
		}
		testLogging {
			events /*'passed',*/ 'skipped', 'failed'
		}
	}
}
version = '1.0'

